if (NOT DEFINED ROLLOUT_SDK_ROOT)
    if (DEFINED $ENV{ROLLOUT_SDK_ROOT})
        set(ROLLOUT_SDK_ROOT $ENV{ROLLOUT_SDK_ROOT})
    endif ()
endif ()

if (NOT "${ROLLOUT_SDK_ROOT}" STREQUAL "")
    list(APPEND CMAKE_PREFIX_PATH ${ROLLOUT_SDK_ROOT})
else ()
    message(WARNING "ROLLOUT_SDK_ROOT is not defined. Looking inside system directories.")
endif ()

FIND_PATH(ROLLOUT_INCLUDE_DIR rollout.h)
FIND_LIBRARY(ROLLOUT_LIBRARY NAMES rollout)
SET(ROLLOUT_TARGET_NAME ROLLOUT::SDK)

# Handle the QUIETLY and REQUIRED arguments and set ROLLOUT_FOUND to TRUE if all listed variables are TRUE.
INCLUDE(FindPackageHandleStandardArgs)

FIND_PACKAGE_HANDLE_STANDARD_ARGS(ROLLOUT REQUIRED_VARS ROLLOUT_LIBRARY ROLLOUT_INCLUDE_DIR)

# Copy the results to the output variables.
if (ROLLOUT_FOUND)
    SET(ROLLOUT_LIBRARIES ${ROLLOUT_LIBRARY})
    SET(ROLLOUT_INCLUDE_DIRS ${ROLLOUT_INCLUDE_DIR})
    if (NOT TARGET ${ROLLOUT_TARGET_NAME})
        add_library(${ROLLOUT_TARGET_NAME} UNKNOWN IMPORTED)
        set_target_properties(${ROLLOUT_TARGET_NAME} PROPERTIES
                INTERFACE_INCLUDE_DIRECTORIES "${ROLLOUT_INCLUDE_DIR}"
                IMPORTED_LOCATION "${ROLLOUT_LIBRARY}")
    endif ()
else (ROLLOUT_FOUND)
    SET(ROLLOUT_LIBRARIES)
    SET(ROLLOUT_INCLUDE_DIRS)
endif (ROLLOUT_FOUND)

MARK_AS_ADVANCED(ROLLOUT_INCLUDE_DIRS ROLLOUT_LIBRARIES)